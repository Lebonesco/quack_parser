/* Lexical Parser */

!whitespace : '\t' | '\n' | '\r' | ' ' ;

_letter : 'A'-'Z' | 'a'-'z' | '_' ;
_digit : '0'-'9' ;
_alpha : _letter | _digit ;

class : 'c' 'l' 'a' 's' 's' ;
def : 'd' 'e' 'f' ;
let: 'l' 'e' 't' ;
extends : 'e' 'x' 't' 'e' 'n' 'd' 's';
if : 'i' 'f' ;
elif : 'e' 'l' 'i' 'f' ;
else : 'e' 'l' 's' 'e' ;
while : 'w' 'h' 'i' 'l' 'e' ;
return : 'r' 'e' 't' 'u' 'r' 'n' ;
typecase : 't' 'y' 'p' 'e' 'c' 'a' 's' 'e' ;
true : 't' 'r' 'u' 'e' ;
false : 'f' 'a' 'l' 's' 'e' ;
or : 'o' 'r' ;
not : 'n' 'o' 't' ;
and : 'a' 'n' 'd' ;
this : 't' 'h' 'i' 's';

/* predefined identifiers */
_valid_escape : '\\' ( '0' | 'b' | 't' | 'n' | 'r' | 'f' | '"' | '\\') ;
_not_escape : 'a' | 'c' | 'd' | 'e' | 'g' | 'h' | 'i' | 'j' | 'k' | 'l'
					| 'm' | 'o' | 'p' | 'q' | 's' | 'u' | 'v' | 'w' 
					| 'x' | 'y' | 'z' ;
string_escape_error : '"'{(_alpha | ' ')}  '\\' _not_escape {(_alpha | ' ')}  '"' ;
//string_newline_error : '"' {.} '\\' 'n' ;
string_literal : '"' {(_alpha | ' ' | '{' | '}' | '[' | ']' | '~' | ',' | '.' | ';' | ':' | '"' '"' { . | '\\' | ' '} '"' '"'
						| '^' | '!' | '@' | '#' | '$' | '%' | '&' | '*' | '(' | ')' | '-' | '?' | '<' | '>' | '|' | '=' | '/' | '\\' [ '0' | 'b' | 't' | 'n' | 'r' | 'f' | '\\' | '"']
						| '+')} '"' ;
int : '0' | '1'-'9' {_digit} ;

ident : 'S' 't' 'r' 'i' 'n' 'g' | 'I' 'n' 't' | 'O' 'b' 'j' | 'B' 'o' 'o' 'l' 'e' 'a' 'n'
		| 'N' 'o' 't' 'h' 'i' 'n' 'g' | 'n' 'o' 'n' 'e' | _letter {_alpha} ;

// --- [ Comments (suppressed) ] -----------------------------------------------
!comment      : _lineComment | _blockComment ;
_lineComment  : '/' '/' { . } '\n' ;
_blockComment : '/' '*' { . | '*' } '*' '/' ;
_tripleQuoteComment : '"' '"' '"' { . } '"' '"' '"' ;

/* punctuation */
plus : '+' ;
minus : '-' ;
mul : '*' ;
div : '/' ;

eq : '=' '=' ;
neq : '!' '=' ;
atmost : '<' '=' ;
lt : '<' ;
atleast : '>' '=' ;
gt : '>' ;

lbrace : '{' ;
rbrace : '}' ;
assign : '=' ;
lparen : '(' ;
rparen : ')' ;
comma : ',' ;
semicolon : ';' ;
period : '.' ;
colon : ':' ;

unknown : . ; // catch unmatched tokens

/* Syntactic Parser */

<<
import (
	"github.com/Lebonesco/quack_parser/ast"
)
>>

Program 
	: Class Statements << ast.NewProgram($0, $1) >>
	;

Class
	: Class ClassSignature ClassBody	<< ast.AppendClass($0, $1, $2) >>
	| empty								<< ast.NewClass() >>
	;

Statements
	: Statements Statement 	<< ast.AppendStatement($0, $1) >>
	| empty					<< ast.NewStatementList() >>
	;

ClassSignature 
	: class ident lparen FormalArgs rparen Extend << ast.NewClassSignature($1, $3, $5) >>
	;

Extend
	: extends ident << ast.NewExtends($1) >>
	| empty
	;

FormalArgs 
	: ident colon ident FormalArgsList << ast.AppendFormalArgs($0, $2, $3) >> 
	| empty 
	;

FormalArgsList
	: FormalArgsList comma ident colon ident << ast.AppendFormalArgs($2, $4, $0) >> 
	| empty									 << ast.NewFormalArg() >>
	;

ClassBody 
	: lbrace Statements Method rbrace	<< ast.NewClassBody($1, $2) >>
	| error
	;

Method 
	: Method def ident lparen FormalArgs rparen Type StatementBlock << ast.AppendMethod($0, $2, $4, $6, $7) >>
	| empty															<< ast.NewMethod() >>
	;

StatementBlock 
	: lbrace Statements rbrace	<< ast.NewStatementBlock($1) >> 
	| error
	;

Statement 
		: if RExpr StatementBlock IfStatement		<< ast.NewIfStatement($1, $2, $3) >>
		//| error
		| while RExpr StatementBlock				<< ast.NewWhileStatement($1, $2) >>
		| let RExpr Type assign RExpr semicolon		<< ast.NewLetStatement($1, $2, $4) >>
		| RExpr Type assign RExpr semicolon			<< ast.NewLetStatement($0, $1, $3) >>
		| RExpr semicolon							<< ast.NewExpressionStatement($0) >>
		| return semicolon							<< ast.NewReturnExpression(nil) >>
		| return RExpr semicolon					<< ast.NewReturnExpression($1) >>								
		| Typecase
		;

// define variable type
Type
	: colon ident 									<< $1, nil >>
	| empty
	;

IfStatement
	: elif RExpr StatementBlock IfStatement         << ast.NewIfStatement($1, $2, $3) >>
	| else StatementBlock 							<< $1, nil >> 
	| empty
	;

RExpr 
      : RExpr plus Term 							<< ast.NewInfixExpression($0, $1, $2) >>
      | RExpr minus Term 							<< ast.NewInfixExpression($0, $1, $2) >>		
      | RExpr atleast Term							<< ast.NewInfixExpression($0, $1, $2) >>
      | RExpr atmost Term							<< ast.NewInfixExpression($0, $1, $2) >>
      | RExpr lt Term			    				<< ast.NewInfixExpression($0, $1, $2) >>
      | RExpr gt Term								<< ast.NewInfixExpression($0, $1, $2) >>
      | RExpr eq Term								<< ast.NewInfixExpression($0, $1, $2) >>
      | RExpr neq Term								<< ast.NewInfixExpression($0, $1, $2) >>
      | Term									    
      | RExpr period ident 							<< ast.NewClassVariable($0, $2) >>
	  | ident lparen ActualArgs rparen				<< ast.NewFunctionCall($0, $2) >> 
      | RExpr and Term								<< ast.NewBoolExpr($0, $2, "and") >>
      | RExpr or Term    							<< ast.NewBoolExpr($0, $2, "or") >>
      | not Term 									<< ast.NewPrefixExpression($0, $1) >>
      | RExpr period ident lparen ActualArgs rparen	<< ast.NewMethodCall($0, $2, $4) >>		
      | unknown										<< ast.Unknown($0) >>		
      | string_escape_error							<< ast.NewStringEscapeError($0) >>			
      ;

Term 
	: Term mul Factor      	<< ast.NewInfixExpression($0, $1, $2) >>
	| Term div Factor		<< ast.NewInfixExpression($0, $1, $2) >>
	| this period ident 	<< ast.NewClassVariable(nil, $2) >>
	| Factor 				
	| Bool  				<< ast.NewBool($0) >>
	;

Factor
	: lparen RExpr rparen	<< $1, nil >>
	| int 					<< ast.NewIntLiteral($0) >>
	| string_literal        << ast.NewStringLiteral($0) >>
	| minus Factor			<< ast.NewPrefixExpression($0, $1) >>
	| ident                 << ast.NewIdentifier($0) >>
	| error
	;

Bool
	: true                  << true, nil >>
	| false					<< false, nil >>
	;

ActualArgs
	: RExpr ArgsList                           << ast.AppendArgs($1, $0) >>
	| empty 			
	;

ArgsList
	: comma RExpr ArgsList                      << ast.AppendArgs($2, $1) >>
	| empty										<< ast.NewArg() >> 
	;

Typecase 
	: typecase RExpr lbrace TypeAlternative rbrace		<< ast.NewTypecase($1, $3) >>
	;

TypeAlternative
	 : TypeAlternative ident colon ident StatementBlock << ast.AppendTypeAlt($0, $1, $3, $4) >>
	 | empty											<< ast.NewTypeAlt() >>
	 ;