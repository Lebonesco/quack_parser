/* Lexical Parser */

!whitespace : '\t' | '\n' | '\r' | ' ' ;


_letter : 'A'-'Z' | 'a'-'z' | '_' ;
_digit : '0'-'9' ;
_alpha : _letter | _digit ;

class : 'c' 'l' 'a' 's' 's' ;
def : 'd' 'e' 'f' ;
let: 'l' 'e' 't' ;
extends : 'e' 'x' 't' 'e' 'n' 'd' 's';
if : 'i' 'f' ;
elif : 'e' 'l' 'i' 'f' ;
else : 'e' 'l' 's' 'e' ;
while : 'w' 'h' 'i' 'l' 'e' ;
return : 'r' 'e' 't' 'u' 'r' 'n' ;
typecase : 't' 'y' 'p' 'e' 'c' 'a' 's' 'e' ;
true : 't' 'r' 'u' 'e' ;
false : 'f' 'a' 'l' 's' 'e' ;
or : 'o' 'r' ;
not : 'n' 'o' 't' ;
and : 'a' 'n' 'd' ;

/* predefined identifiers */
_valid_escape : '\\' ( '0' | 'b' | 't' | 'n' | 'r' | 'f' | '"' | '\\') ;
_not_escape : 'a' | 'c' | 'd' | 'e' | 'g' | 'h' | 'i' | 'j' | 'k' | 'l'
					| 'm' | 'o' | 'p' | 'q' | 's' | 'u' | 'v' | 'w' 
					| 'x' | 'y' | 'z' ;
string_escape_error : '"'{(_alpha | ' ')}  '\\' _not_escape {(_alpha | ' ')}  '"' ;
//string_newline_error : '"' {.} '\\' 'n' ;
string_literal : '"' {(_alpha | ' ' | '{' | '}' | '[' | ']' | '~' | ',' | '.' | ';' | ':' 
						| '^' | '!' | '@' | '#' | '$' | '%' | '&' | '*' | '(' | ')' | '-' | '?' | '<' | '>' | '|' | '=' | '/'
						| '+')} '"' ;
int : '1'-'9' {_digit} ;

ident : 'S' 't' 'r' 'i' 'n' 'g' | 'I' 'n' 't' | 'O' 'b' 'j' | 'B' 'o' 'o' 'l' 'e' 'a' 'n'
		| 'N' 'o' 't' 'h' 'i' 'n' 'g' | 'n' 'o' 'n' 'e' | _letter {_alpha} ;

// --- [ Comments (suppressed) ] -----------------------------------------------
!comment      : _lineComment | _blockComment | _tripleQuoteComment ;
_lineComment  : '/' '/' { . } '\n' ;
_blockComment : '/' '*' { . | '*' } '*' '/' ;
_tripleQuoteComment : '"' '"' '"' { . } '"' '"' '"' ;

/* punctuation */
plus : '+' ;
minus : '-' ;
mul : '*' ;
div : '/' ;

eq : '=' '=' ;
neq : '!' '=' ;
atmost : '<' '=' ;
lt : '<' ;
atleast : '>' '=' ;
gt : '>' ;

lbrace : '{' ;
rbrace : '}' ;
assign : '=' ;
lparen : '(' ;
rparen : ')' ;
comma : ',' ;
semicolon : ';' ;
period : '.' ;
colon : ':' ;

unknown : . ; // catch unmatched tokens

/* Syntactic Parser */

<<
import (
//	"github.com/Lebonesco/quack_parser/token"
//	"github.com/Lebonesco/quack_parser/util"
	"github.com/Lebonesco/quack_parser/ast"
)
>>

Program 
	: Class Statements << ast.NewProgram($1) >>
	;

Class
	: ClassSignature ClassBody Class
	| empty
	;

Statements
	: Statements Statement 	<< ast.AppendStatement($0, $1) >>
	| Statement 			<< ast.NewStatementList($0) >>
	;

ClassSignature : class ident lparen FormalArgs rparen Extend ;

Extend
	: extends ident
	| empty
	;

FormalArgs 
	: ident colon ident FormalArgsList				
	| empty ;

FormalArgsList
	: comma ident colon ident FormalArgsList		
	| empty
	;

ClassBody : lbrace Statements Method rbrace ;	

Method 
	: def ident lparen FormalArgs rparen Type StatementBlock 
	| empty
	;

StatementBlock : lbrace Statements rbrace ;	

Statement 
		: if RExpr StatementBlock IfStatement
		| while RExpr StatementBlock				
		| let LExpr assign RExpr semicolon			 << ast.NewLetStatement($1, $3) >>
		| RExpr semicolon							 << ast.NewExpressionStatement($0) >>
		| return semicolon
		| Typecase
		;

// define variable type
Type
	: colon ident
	| empty
	;

IfStatement
	: elif RExpr StatementBlock IfStatement
	| else StatementBlock
	| empty
	;

LExpr : ident 										<< ast.NewIdentifier($0) >>
	  | RExpr period ident
	  ;

RExpr 
	  : string_literal								<< ast.NewStringLiteral($0) >>
      | RExpr plus Term 							<< ast.NewInfixExpression($0, $1, $2) >>
      | RExpr minus Term 							<< ast.NewInfixExpression($0, $1, $2) >>
      | RExpr atleast Term							<< ast.NewInfixExpression($0, $1, $2) >>
      | RExpr atmost Term							<< ast.NewInfixExpression($0, $1, $2) >>
      | RExpr lt Term			    				<< ast.NewInfixExpression($0, $1, $2) >>
      | RExpr gt Term								<< ast.NewInfixExpression($0, $1, $2) >>
      | RExpr eq Term								<< ast.NewInfixExpression($0, $1, $2) >>
      | RExpr neq Term								<< ast.NewInfixExpression($0, $1, $2) >>
      | Term
      | ident 										<< ast.NewIdentifier($0) >>
      | RExpr and Bool								<< ast.NewBoolExpr($0, $2, "and") >>
      | RExpr or Bool    							<< ast.NewBoolExpr($0, $2, "or") >>
      | not Bool 									<< !$1.(bool), nil >>
      | Bool  										<< ast.NewBool($0) >>
      | RExpr period ident lparen ActualArgs rparen	
      | ident lparen ActualArgs rparen
      | return RExpr semicolon
      | error
      ;

Term 
	: Term mul Factor      	<< ast.NewInfixExpression($0, $1, $2) >>
	| Term div Factor		<< ast.NewInfixExpression($0, $1, $2) >>
	| Factor 				
	;

Factor
	: lparen RExpr rparen	<< $1, nil >>
	| int 					<< ast.NewIntLiteral($0) >>
	;

Bool
	: true                  << true, nil >>
	| false					<< false, nil >>
	;

ActualArgs
	: RExpr ArgsList
	| empty 
	;

ArgsList
	: comma RExpr ArgsList
	| empty
	;

Typecase : typecase RExpr lbrace TypeAlternative rbrace ;

TypeAlternative
	 : ident colon ident StatementBlock TypeAlternative
	 | empty
	 ;